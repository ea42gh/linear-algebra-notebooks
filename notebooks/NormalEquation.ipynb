{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "d721fbf4-b966-4be8-8432-0dc889f932f6",
   "metadata": {
    "tags": []
   },
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "\u001b[32m\u001b[1m  Activating\u001b[22m\u001b[39m project at `C:\\Users\\jeff\\NOTEBOOKS\\elementary-linear-algebra\\GenLinAlgProblems`\n"
     ]
    }
   ],
   "source": [
    "using Pkg\n",
    "Pkg.activate(\"../GenLinAlgProblems/\")\n",
    "\n",
    "using Statistics, LinearAlgebra, Random, Latexify\n",
    "using GenLinAlgProblems"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "eefd3de3-b975-4fde-8bd5-e7711488007d",
   "metadata": {},
   "outputs": [],
   "source": [
    "approx(A;d=3)=(x->round(x,digits=d)).(A);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1dba7185-8a24-4d32-aa8b-11685049a32d",
   "metadata": {},
   "source": [
    "<div style=\"float:center;width:100%;text-align: center;\"><strong style=\"height:60px;color:darkred;font-size:40px;\">Solving the Normal Equations</strong></div>"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "3be6a1d8-0fd5-4d80-b0da-5e0b3c623dff",
   "metadata": {
    "tags": []
   },
   "source": [
    "# 0. Generate Some Data"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "815c6df5-4245-4867-8c6c-4b5dc113e6c5",
   "metadata": {},
   "outputs": [],
   "source": [
    "M=40; N=3\n",
    "\n",
    "Random.seed!(24526)\n",
    "\n",
    "A = 10*(rand(M,N ) .- 0.5)\n",
    "b =  4*(rand(M)    .- 0.5)\n",
    "data_dict = Dict( \"b\" => b )\n",
    "for i ∈ 1:N\n",
    "    data_dict[\"a_$i\"] = A[:,i]\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9bdf9623-e486-4878-85a4-9d93d2a36e19",
   "metadata": {},
   "source": [
    "# 1. Gaussian Elimination  applied to $\\mathbf{\\left( A^t A \\mid A^t b \\right)}$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "796a50c2-ada5-4c83-9f8b-70a414e85845",
   "metadata": {},
   "source": [
    "Given $A x = b$, we can decompose $b$ into two othogonal vectors $b = b_{//} + b_\\perp,$\n",
    "where $b_{//} \\in \\mathscr{C}(A)$ and $b_\\perp \\in \\mathscr{N}(A^t)$\n",
    "\n",
    "$\\qquad\\begin{align}\n",
    "& A^t A x &=&\\ A^t b \\qquad\\qquad & \\text{normal equations} \\\\\n",
    "& b_{//}  &=&\\ A x                & \\text{parallel component of } b \\\\\n",
    "& b_\\perp &=&\\ b - b_{//}         & \\text{orthogonal component of } b \\\\\n",
    "\\end{align}$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "af14c942-8c92-4d75-8e4d-522570a22a47",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/latex": [
       "\\begin{equation}\n",
       "\\left[\n",
       "\\begin{array}{cccc}\n",
       "A^t A =  & \\left[\n",
       "\\begin{array}{ccc}\n",
       "278.471 & -31.409 & -46.007 \\\\\n",
       "-31.409 & 342.112 & -18.326 \\\\\n",
       "-46.007 & -18.326 & 450.713 \\\\\n",
       "\\end{array}\n",
       "\\right] & A^t b = & \\left[\n",
       "\\begin{array}{c}\n",
       "-12.753 \\\\\n",
       "24.782 \\\\\n",
       "9.983 \\\\\n",
       "\\end{array}\n",
       "\\right] \\\\\n",
       "\\end{array}\n",
       "\\right]\n",
       "\\end{equation}\n"
      ],
      "text/plain": [
       "L\"\\begin{equation}\n",
       "\\left[\n",
       "\\begin{array}{cccc}\n",
       "A^t A =  & \\left[\n",
       "\\begin{array}{ccc}\n",
       "278.471 & -31.409 & -46.007 \\\\\n",
       "-31.409 & 342.112 & -18.326 \\\\\n",
       "-46.007 & -18.326 & 450.713 \\\\\n",
       "\\end{array}\n",
       "\\right] & A^t b = & \\left[\n",
       "\\begin{array}{c}\n",
       "-12.753 \\\\\n",
       "24.782 \\\\\n",
       "9.983 \\\\\n",
       "\\end{array}\n",
       "\\right] \\\\\n",
       "\\end{array}\n",
       "\\right]\n",
       "\\end{equation}\n",
       "\""
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "latexify( [ latex(\"A^t A = \"), approx(A'A), latex(\"A^t b =\"), approx(A'b)']')"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "b0defb93-dcf9-4a36-9c74-3cf722457a5e",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Solve the normal equation for x ≈ [-0.034, 0.07, 0.022]\n",
      "Check orthogonality: b_parallel^t b_perp = 0.0\n"
     ]
    }
   ],
   "source": [
    "AtA = A'A\n",
    "Atb = A'b\n",
    "x   = AtA \\ Atb\n",
    "\n",
    "b_parallel = A*x\n",
    "b_perp     = b - b_parallel\n",
    "\n",
    "println( \"Solve the normal equation for x ≈ $(round.(x,digits=3))\")\n",
    "println(\"Check orthogonality: b_parallel^t b_perp = \", round(dot(b_parallel, b_perp), digits=10))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8eaba9ef-5fee-40d9-8c13-cf155d3bd54d",
   "metadata": {},
   "source": [
    "#### Orthogonal Projection onto $\\mathbf{\\mathscr{C}(A)}$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "91fb10b6-6704-4672-8483-8b4d907c86c9",
   "metadata": {},
   "source": [
    "Assuming we have thrown out all free variable columns in $A$,<br> we can solve for $ b_{parallel} = A \\left( A^t A \\right)^{-1} A^t b$.\n",
    "\n",
    "The **orthogonal projection matrix** onto $\\mathscr{C}(A)$ is given by $\n",
    "P_{parallel} = A \\left( A^t A \\right)^{-1} A^t$\n",
    "\n",
    "**Remark:** Do not compute the inverse to compute $P_{parallel}$:<br>\n",
    "$\\quad$ Since $\\left( A^t A \\right)^{-1} A^t = X \\Leftrightarrow (A^t A) X = A^t$\n",
    "* First, solve $(A^t A) X = A^t$ for $X$\n",
    "* Then $P_{parallel} = A X$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "774c4f05-29dd-496b-97b6-045419bde7b4",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "P_parallel[1:5,1:5] ≈ \n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "5×5 Matrix{Float64}:\n",
       "  0.1007   0.0333   0.0325   0.0021  -0.0168\n",
       "  0.0333   0.0689  -0.0617  -0.0566   0.0314\n",
       "  0.0325  -0.0617   0.105    0.0736  -0.0522\n",
       "  0.0021  -0.0566   0.0736   0.0571  -0.0371\n",
       " -0.0168   0.0314  -0.0522  -0.0371   0.0265"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "X = AtA \\ A'\n",
    "P_parallel = A*X\n",
    "#P_parallel = A*inv(A'*A)* A'\n",
    "println( \"P_parallel[1:5,1:5] ≈ \")\n",
    "approx( P_parallel[1:5,1:5], d=4)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "dca4d8e5-023c-427b-92ed-4425ca3ed8f4",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "P_parallel ^ 2 ≈ P_parallel = true\n"
     ]
    }
   ],
   "source": [
    "# check P_parallel ^2 ≈ P_parallel\n",
    "@show ( P_parallel^2 ≈ P_parallel);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0dde0000-68bf-4c84-b5a5-d450d37858d3",
   "metadata": {},
   "source": [
    "> **Question:**\n",
    "> * What happens when $A$ has more columns than rows?<br> (Or more generally, when $A$ does not have full column rank?)\n",
    "> * What is the size of $A^t A$ compared to the size of $A$?\n",
    "> * What is the condition number of $A^t A$ compared to the condition number of $A$?"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "176f8c6a-2b26-411f-bbfb-597953f4fbac",
   "metadata": {},
   "source": [
    "# 2. Using the $\\mathbf{QR}$ Decomposition"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5ce2495c-9990-4c66-9827-f7421515a60a",
   "metadata": {},
   "source": [
    "The normal equations become $R x = Q^t b$.\n",
    "\n",
    "The **projection matrix** is $P_{parallel} = Q Q^t$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "407dd42f-f4da-4a49-b8db-cdf6608d9a28",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "R ≈\n"
     ]
    },
    {
     "data": {
      "text/latex": [
       "\\begin{equation}\n",
       "\\left[\n",
       "\\begin{array}{ccc}\n",
       "16.6874 & -1.8822 & -2.757 \\\\\n",
       "0.0 & 18.4003 & -1.278 \\\\\n",
       "0.0 & 0.0 & 21.0114 \\\\\n",
       "\\end{array}\n",
       "\\right]\n",
       "\\end{equation}\n"
      ],
      "text/plain": [
       "L\"\\begin{equation}\n",
       "\\left[\n",
       "\\begin{array}{ccc}\n",
       "16.6874 & -1.8822 & -2.757 \\\\\n",
       "0.0 & 18.4003 & -1.278 \\\\\n",
       "0.0 & 0.0 & 21.0114 \\\\\n",
       "\\end{array}\n",
       "\\right]\n",
       "\\end{equation}\n",
       "\""
      ]
     },
     "execution_count": 8,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "Q,R = qr(A);\n",
    "Q = Matrix(Q)\n",
    "println(\"R ≈\")\n",
    "latexify(approx(R, d=4))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "a0d4606a-e166-4e85-be24-892dd2c73de0",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Solve the normal equation for x ≈ [-0.034, 0.07, 0.022]\n",
      "Solve the R x = Qᵗ b equation x ≈ [-0.034, 0.07, 0.022]\n"
     ]
    }
   ],
   "source": [
    "#x_qr = inv(R) * Q' * b   # thus, Q'b = R x_qr\n",
    "x_qr = R \\ Q'b\n",
    "println( \"Solve the normal equation for x ≈ $(round.(x,digits=3))\")\n",
    "println( \"Solve the R x = Qᵗ b equation x ≈ $(round.(x_qr,digits=3))\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "99248af5-acc2-44cb-af2c-da72505dfd8e",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "P_parallel ≈ P_parallel_qr = true\n"
     ]
    }
   ],
   "source": [
    "P_parallel_qr = Q*Q'\n",
    "@show (P_parallel ≈ P_parallel_qr);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e9f3eb62-fd46-470e-9a26-c35da45544fb",
   "metadata": {},
   "source": [
    "# 3. Using the SVD"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f4a202de-ab61-4d24-bd39-9aaa989d9e10",
   "metadata": {},
   "source": [
    "The solution of the normal equation is $x = A^\\dagger b$.<br>\n",
    "The orhogonal projection matrix is $A A^\\dagger = U U^t = U_r U_r^t$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "96458f87-51cb-4811-a5bd-7223acb7fdc8",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "the pseudoinverse[:,1:5] is given by\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "3×5 Matrix{Float64}:\n",
       " -0.0     0.006  -0.012  -0.008   0.005\n",
       "  0.013  -0.003   0.012   0.007  -0.006\n",
       "  0.01    0.011  -0.006  -0.008   0.003"
      ]
     },
     "execution_count": 11,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "Adagger = pinv(A)\n",
    "println( \"the pseudoinverse[:,1:5] is given by\")\n",
    "approx(Adagger[:,1:5], d=3)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "501da248-2f62-4d8d-9fa2-69c51c8bbc3f",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Solve the normal equation for x ≈ [-0.034, 0.07, 0.022]\n",
      "Solve the R x = Qᵗ b equation x ≈ [-0.034, 0.07, 0.022]\n"
     ]
    }
   ],
   "source": [
    "x_svd = Adagger*b\n",
    "println( \"Solve the normal equation for x ≈ $(round.(x,digits=3))\")\n",
    "println( \"Solve the R x = Qᵗ b equation x ≈ $(round.(x_svd,digits=3))\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "id": "786590d9-893c-40a7-af39-06ca6e6842fc",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "P_parallel ≈ P_parallel_svd = true\n"
     ]
    }
   ],
   "source": [
    "P_parallel_svd = A * Adagger\n",
    "@show P_parallel ≈ P_parallel_svd;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "id": "2de766b7-4dbb-4ed5-a11e-ce6af03edc8f",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "P_parallel ≈ U * U' = true\n"
     ]
    }
   ],
   "source": [
    "U = svd(A).U\n",
    "@show P_parallel ≈ U*U';"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.1",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
